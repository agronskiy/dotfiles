#!/usr/bin/env zsh

# Two-phase filtering with Ripgrep and fzf
#
# 1. Search for text in files using Ripgrep
# 2. Interactively restart Ripgrep with reload action
#    * Press alt-enter to switch to fzf-only filtering
# 3. Open the file in Vim or print path

# -m : multiline ripgrep

while getopts m opt; do
  case $opt in
    m) MULTILINE_OPT_ARG="SET"
    ;;
  esac
done

while :; do
  if [ ! $# -eq 0 ] && [[ "$1" =~ ^\-.* ]] ; then
    shift
  else
    break
  fi
done

if [ ! -z ${MULTILINE_OPT_ARG+x} ] ; then
  RG_PREFIX="rg --multiline --multiline-dotall "
else
  RG_PREFIX="rg "
fi

RG_PREFIX="$RG_PREFIX --column --line-number --no-heading --color=always --smart-case "
INITIAL_QUERY="${*:-}"

output=$(
  FZF_DEFAULT_COMMAND="$RG_PREFIX $(printf %q "$INITIAL_QUERY")" \
  FZF_TMUX_OPTS="$FZF_TMUX_OPTS -p 95%,95%" \
  FZF_DEFAULT_OPTS="$FZF_DEFAULT_OPTS --height 90%" \
  fzf-tmux $FZF_TMUX_OPTS --ansi \
      --color "hl:-1:underline,hl+:-1:underline:reverse" \
      --disabled --query "$INITIAL_QUERY" \
      --bind "change:reload:sleep 0.1; $RG_PREFIX {q} || true" \
      --bind "alt-enter:unbind(change,alt-enter)+change-prompt(2. fzf> )+enable-search+clear-query" \
      --prompt '1. ripgrep> ' \
      --delimiter : \
      --preview 'bat --style=numbers,header --color=always {1} --highlight-line {2}' \
      --preview-window 'up,75%,border,+{2}+3/3,~3'
)
# output is `file_path:line:col:text_of_line`
selected=(${(s/:/)output})

if [ "$TERM_PROGRAM" = "vscode" ]; then
  [ -n "${selected[1]}" ] && code --goto "${selected[1]}:${selected[2]}:${selected[3]}"
else
  [ -n "${selected[1]}" ] && vim "${selected[1]}" "+${selected[2]}"
fi

