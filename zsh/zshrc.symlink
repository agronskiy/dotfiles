
# Shortcut to this dotfile's path is $DOTFILES.
# your actual dotfile repo in 'work/dotfiles'?
if [[ -d "${HOME}/.dotfiles" ]]; then
  export DOTFILES="${HOME}/.dotfiles"
else
  echo "Can't locate .dotfiles!"
  exit 1
fi

source $DOTFILES/zsh/oh-my-zsh.explicit-load.zsh

# Avoid closing terinal on ctrl-d
set -o ignoreeof

# Added by fzf
# NOTE(agronskiy): run before any other sourcing because our commands (such as custom history widget) will redefine some
# commands/bingings.
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# Conda stuff
# >>> conda initialize >>>
# !! Contents within this block are managed by 'conda init' !!
__conda_setup="$('${HOME}/miniconda3/bin/conda' 'shell.zsh' 'hook' 2> /dev/null)"
if [ $? -eq 0 ]; then
    eval "$__conda_setup"
else
    if [ -f "${HOME}/miniconda3/etc/profile.d/conda.sh" ]; then
        . "${HOME}/miniconda3/etc/profile.d/conda.sh"
    else
        export PATH="${HOME}/miniconda3/bin:$PATH"
    fi
fi
unset __conda_setup
# <<< conda initialize <<<

# For iPython Notebook issue - need to set the locale, but not all (?)
export LC_ALL=en_US.UTF-8
export LANG=en_US.UTF-8

export TERM=xterm-256color

# This allows grepping with colors
export GREP_COLOR='1;35;40'

# Logging
source $DOTFILES/zsh/logging.explicit-load.zsh

# Loading the stuff from topic folders
# Config files are all the files with *.zsh except for those with *.explicit-load.zsh
# The latter are meant to be loaded somewhere explicitly (for prioritization control)
declare -U config_files
config_files=($DOTFILES/**/*.zsh)
config_files=(${config_files:#*/*.explicit-load.zsh})

# Load the *.pre-load.zsh files. E.g. paths go here.
for file in ${(M)config_files:#*/*.pre-load.zsh}; do
    source $file
done

# Load everything but the *.pre-load.zsh and *.post-load.zsh
for file in ${${config_files:#*/*.pre-load.zsh}:#*/*.post-load.zsh}; do
    source $file
done


# Initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit && compinit

# Support bash-style autocompletion, not just zsh-style
autoload -U +X bashcompinit && bashcompinit
bashcompinit

# Colors
autoload -U colors && colors

# Load things that must go afterwards. E.g. various options for autocomplete go here.
for file in ${(M)config_files:#*/*.post-load.zsh}; do
    source $file
done

# Stash local environment variables in ~/.localrc. This means they'll stay out
# of the main dotfiles repository (which is public).
if [[ -a ~/.localrc ]]; then
    source ~/.localrc
fi
unset config_files

export ALREADY_SOURCED_ZSHRC=1
